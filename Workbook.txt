' ワークブック(Excel)を開く
' 既に開かれている場合は、WBオブジェクトを返す。
'
' FileName : フルパスのファイル名
' Retval   : 0 = 正常終了(ファイルを開いてWBオブジェクトを返す)
'            1 = 正常終了(既に開いているWBオブジェクトを返す)
'            3 = ファイルなし (Nothing返す)
'            4 = ファルダなし (Nothing返す)
'            Other = Err.Number
' 戻り値   : 開いたWorkbookオブジェクト
'            Nothing = 見つからず'
' 参照設定 : Microsoft Scripting Runtime
'
Public Function WBOpenX(FPName As String, ByRef Retval As Long) As Workbook
    On Error GoTo ErrProc
    Dim FSO As FileSystemObject
    Dim s As String

    Set FSO = New FileSystemObject

    ' フォルダの存在チェック
    s = FSO.GetParentFolderName(FPName)
    If Not FSO.FolderExists(s) Then Retval = 4: GoTo EndProc

    ' ファイルの存在チェック
    If Not FSO.FileExists(FPName) Then Retval = 3: GoTo EndProc
    
    Set WBOpenX = IsWBOpen(FPName, 2)   ' 既に開かれているかチェック
    If Not WBOpenX Is Nothing Then
        Retval = 1  ' 既に開いていた
    Else
        Set WBOpenX = Workbooks.Open(FPName)    ' ファイルを開く
    End If
EndProc:
    Set FSO = Nothing
    Exit Function

ErrProc:
    Retval = Err.Number
    GoTo EndProc
End Function

' ワークブックが開いているか確認
' FPName : フルパスのファイル名
' mode     : 1 = Workbookのみ (既定値)
'            2 = Workbook + AddIn
' 戻り値   : 該当するWorkbookオブジェクト
'            Nothing : 見つからず
'
Public Function IsWBOpen(FPName As String, _
        Optional mode As Long = 1) As Workbook
    Dim WB As Workbook
    Dim AI As AddIn
    Dim s As String
    
    'モード1: 通常のワークブックのみをチェック
    For Each WB In Workbooks
'        s = ConvODPath(WB.FullName) ' OneDrive内のパスをローカルパスに変換
'        If LCase(s) = LCase(FPName) Then
        If LCase(WB.FullName) = LCase(FPName) Then
            Set IsWBOpen = WB
            Exit Function
        End If
    Next WB

    'モード2: アドインもチェック
    If mode <> 2 Then GoTo EndProc
    For Each AI In AddIns
'        s = ConvODPath(AI.FullName) ' OneDrive内のパスをローカルパスに変換
'        If LCase(s) = LCase(FPName) Then
        If LCase(AI.FullName) = LCase(FPName) Then
            On Error Resume Next ' アドインが非表示の場合のエラーを無視
            Set IsWBOpen = Workbooks(AI.Name)
            On Error GoTo 0
            If Not IsWBOpen Is Nothing Then Exit Function
        End If
    Next AI
EndProc:
    Set IsWBOpen = Nothing
End Function

' 新規Workbookを作成 (表示・非表示設定付)
' Visible : True  = Workbookを表示で作成 (既定値)
'           False = Workbookを非表示で作成
' 戻り値  : 作成した新規WBオブジェクト
'           Nothing = エラー発生
' 参照設定:Microsoft Excel xx.x Object Library
' 注：既存のExcelインスタンスがあれば、それに対して、
'     Workbookを作成。なければ新しいインスタンスを作成。
'
Public Function WBOpenNew(Optional Visible As Boolean = True) As Workbook
    Dim xlApp As Excel.Application
    Dim xlWB As Excel.Workbook
    
    On Error Resume Next
    Set xlApp = GetObject(, "Excel.Application")
    If Err.Number <> 0 Then
        Err.Clear
        Set xlApp = New Excel.Application
    End If
    xlApp.Visible = Visible
    
    On Error GoTo EndProc
    Set xlWB = xlApp.Workbooks.Add
    Set WBOpenNew = xlWB
EndProc:
End Function
